--USE [5377_GOLDBERG_II_R2];
--USE [5377_WEIGAND_II_R2];
GO

CREATE OR ALTER PROCEDURE filevine.usp_insert_reference_ProjectCustomFieldMetadata
	@DEBUGFLAG             BIT,
	@DATABASE              VARCHAR(1000),
	@SCHEMANAME            VARCHAR(1000),
	@FVPRODUCTIONPREFIX    VARCHAR(1000),
	@TIMEZONE              VARCHAR(1000)
AS
BEGIN

	DROP TABLE IF EXISTS 
		DBO.__FV_PROJECTCUSTOMFIELDMETADATA;

	CREATE TABLE [dbo].[__FV_ProjectCustomFieldMetadata]
	(
		PROJECTID             VARCHAR(MAX) NULL,
		PROJECTTYPE           VARCHAR(MAX) NOT NULL,
		SECTION               VARCHAR(MAX) NOT NULL,
		ISCOLLECTION          VARCHAR(MAX) NOT NULL,
		FIELDPOSITION         VARCHAR(MAX) NOT NULL,
		FIELDSELECTOR         VARCHAR(MAX) NOT NULL,
		DISPLAYNAME           VARCHAR(MAX) NOT NULL,
		FIELDTYPE             VARCHAR(MAX) NOT NULL,
		DATATYPE              VARCHAR(MAX) NOT NULL,
		FIELDID               VARCHAR(MAX) NOT NULL,
		COLLECTIONITEMGUID    VARCHAR(MAX) NULL,
		[VALUE]               VARCHAR(MAX) NULL,
	);

	WITH PROJECTSECTIONFIELDS
		 AS (SELECT 
				 F.ID AS FIELDID,
				 LEFT(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(PT.NAME,'/',' '),'&',' '),'(',' '),')',' '),' ',''),20) AS PROJECTTYPE,
				 LEFT(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(S.NAME,'/',' '),'&',' '),'(',' '),')',' '),' ',''),20) AS SECTION,
				 S.ISCOLLECTION AS ISCOLLECTION,
				 CONCAT('(',RIGHT('0000' + CONVERT(VARCHAR(20),F.ROW),4),',',RIGHT('0000' + CONVERT(VARCHAR(20),F.ORDERINROW),4),')') AS FIELDPOSITION,
				 F.FIELDSELECTOR AS FIELDSELECTOR,
				 F.NAME AS DISPLAYNAME,
				 F.CUSTOMFIELDTYPE
			 FROM DBO.CUSTOMPROJECTTYPE AS PT
			 INNER JOIN DBO.CUSTOMSECTION AS S
				 ON S.CUSTOMPROJECTTYPEID = PT.ID
			 INNER JOIN DBO.CUSTOMFIELD AS F
				 ON F.CUSTOMSECTIONID = S.ID
			 /*Including only field types that we can migrate into*/
			 WHERE F.CUSTOMFIELDTYPE IN
				 (
				 01,	/*Integer = 1				->	CustomDataInteger	*/
				 02,	/*Currency = 2				->	CustomDataDecimal	*/
				 03,	/*String = 3				->	CustomDataString	*/
				 04,	/*Date = 4					->	CustomDataDate		*/
				 05,	/*Boolean = 5				->	CustomDataBoolean	*/
				 06,	/*Text = 6					->	CustomDataString	*/
				 07,	/*PersonLink = 7			->	CustomDataPerson	*/
				 08,	/*Doc = 8					->	CustomDataDoc		*/
				 09,	/*Dropdown = 9				->	CustomDataString	*/
				 11,	/*DocList = 11				->	CustomDataDocList	*/
				 13,	/*PlainDecimal = 13			->	CustomDataDecimal	*/
				 14,	/*Percent = 14				->	CustomDataDecimal	*/
				 15,	/*Deadline = 15				->	CustomDataDeadline	*/
				 17,	/*TextLarge = 17			->	CustomDataString	*/
				 24,	/*StringList = 24			->	CustomDataStringList*/
				 25,	/*PersonList = 25			->	CustomDataPersonList*/
				 26		/*MultiSelectList = 26		->	CustomDataStringList*/
				 ))
		 INSERT INTO DBO.__FV_PROJECTCUSTOMFIELDMETADATA
		 (
			 PROJECTID,
			 PROJECTTYPE,
			 SECTION,
			 ISCOLLECTION,
			 FIELDPOSITION,
			 FIELDSELECTOR,
			 DISPLAYNAME,
			 FIELDTYPE,
			 DATATYPE,
			 FIELDID,
			 COLLECTIONITEMGUID,
			 [VALUE]
		 )
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'BOOLEAN' AS FIELDTYPE,
			 'BIT' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.BOOLVALUE) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATABOOLEAN AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE = 5
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'DATE' AS FIELDTYPE,
			 'DATETIME' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.DATEVALUE,121) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATADATE AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE = 4
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR + 'DUEDATE' AS FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'DEADLINEDUEDATE' AS FIELDTYPE,
			 'SMALLDATETIME' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.DATEVALUE,121) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATADEADLINE AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE = 15
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR + 'DONEDATE' AS FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'DEADLINEDONEDATE' AS FIELDTYPE,
			 'SMALLDATETIME' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.DONEDATE,121) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATADEADLINE AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE = 15
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'DECIMAL' AS FIELDTYPE,
			 'DECIMAL(18,2)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.DECIMALVALUE) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATADECIMAL AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE IN
			 (
			 2,
			 13,
			 14
			 )
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'DOC' AS FIELDTYPE,
			 'VARCHAR(MAX)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),ND.DOCEXTERNALID) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATADOC AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 LEFT JOIN PT1.DOCUMENTS AS ND
			 ON ND.DOCEXTERNALID = CONVERT(VARCHAR(MAX),D.DOCID)
		 WHERE F.CUSTOMFIELDTYPE = 8
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'DOCLIST' AS FIELDTYPE,
			 'VARCHAR(MAX)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),DL.VALUECSV) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATADOCLIST AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 OUTER APPLY
		 (
			 SELECT 
				 STUFF(
			 (
				 SELECT TOP 100 PERCENT 
					 ',' + CONVERT(VARCHAR(MAX),ND.DOCEXTERNALID)
				 FROM DBO.CUSTOMDATADOCLISTITEM AS DLI
				 INNER JOIN PT1.DOCUMENTS AS ND
					 ON ND.DOCEXTERNALID = CONVERT(VARCHAR(MAX),DLI.DOCID)
				 WHERE DLI.COLLECTIONITEMGUID = D.COLLECTIONITEMGUID AND DLI.PROJECTID = D.PROJECTID
				 ORDER BY 
					 DLI.COLLECTIONITEMGUID FOR
				 XML PATH('')
			 ),1,1,'') AS VALUECSV
		 ) AS DL
		 WHERE F.CUSTOMFIELDTYPE = 11
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'INTEGER' AS FIELDTYPE,
			 'INT' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.INTVALUE) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATAINTEGER AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE = 1
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'PERSON' AS FIELDTYPE,
			 'VARCHAR(MAX)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),ND.CONTACTCUSTOMEXTERNALID) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATAPERSON AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 LEFT JOIN PT1.CONTACTSCUSTOM__CONTACTINFO AS ND
			 ON ND.CONTACTCUSTOMEXTERNALID = CONVERT(VARCHAR(MAX),D.PERSONID)
		 WHERE F.CUSTOMFIELDTYPE = 7
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'PERSONLIST' AS FIELDTYPE,
			 'VARCHAR(MAX)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),DL.VALUECSV) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATAPERSONLIST AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 OUTER APPLY
		 (
			 SELECT 
				 STUFF(
			 (
				 SELECT TOP 100 PERCENT 
					 ',' + CONVERT(VARCHAR(MAX),ND.CONTACTCUSTOMEXTERNALID)
				 FROM DBO.CUSTOMDATAPERSONLISTITEM AS DLI
				 INNER JOIN PT1.CONTACTSCUSTOM__CONTACTINFO AS ND
					 ON ND.CONTACTCUSTOMEXTERNALID = CONVERT(VARCHAR(MAX),DLI.PERSONID)
				 WHERE DLI.COLLECTIONITEMGUID = D.COLLECTIONITEMGUID AND DLI.PROJECTID = D.PROJECTID
				 ORDER BY 
					 DLI.POSITION FOR
				 XML PATH('')
			 ),1,1,'') AS VALUECSV
		 ) AS DL
		 WHERE F.CUSTOMFIELDTYPE = 25
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'STRING' AS FIELDTYPE,
			 'NVARCHAR(MAX)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),D.STRINGVALUE) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATASTRING AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 WHERE F.CUSTOMFIELDTYPE IN
			 (
			 3,
			 6,
			 9,
			 17
			 )
		 UNION ALL
		 SELECT 
			 D.PROJECTID,
			 F.PROJECTTYPE,
			 F.SECTION,
			 F.ISCOLLECTION,
			 F.FIELDPOSITION,
			 F.FIELDSELECTOR,
			 F.DISPLAYNAME,
			 'STRINGLIST' AS FIELDTYPE,
			 'NVARCHAR(MAX)' AS DATATYPE,
			 F.FIELDID,
			 D.COLLECTIONITEMGUID,
			 CONVERT(VARCHAR(MAX),DL.VALUECSV) AS VALUE
		 FROM PROJECTSECTIONFIELDS AS F
		 LEFT JOIN DBO.CUSTOMDATASTRINGLIST AS D
			 ON D.CUSTOMFIELDID = F.FIELDID
		 OUTER APPLY
		 (
			 SELECT 
				 STUFF(
			 (
				 SELECT 
					 ',' + CONVERT(VARCHAR(MAX),DLI.STRINGVALUE)
				 FROM DBO.CUSTOMDATASTRINGLISTITEM AS DLI
				 WHERE DLI.COLLECTIONITEMGUID = D.COLLECTIONITEMGUID AND DLI.PROJECTID = D.PROJECTID
				 ORDER BY 
					 DLI.STRINGVALUE FOR
				 XML PATH('')
			 ),1,1,'') AS VALUECSV
		 ) AS DL
		 WHERE F.CUSTOMFIELDTYPE IN
			 (
			 24,
			 26
			 );

	/*Output Data Report about all ProjectType sections*/
	SELECT 
		PROJECTTYPE,
		SECTION,
		COUNT(DISTINCT FIELDSELECTOR) AS FIELDCOUNT,
		COUNT(*) AS RECORDCOUNT,
		SUM(IIF(VALUE IS NOT NULL,1.0,0.0)) / COUNT(*) AS NOTNULLPERCENT
	FROM __FV_PROJECTCUSTOMFIELDMETADATA
	GROUP BY 
		PROJECTTYPE,
		SECTION
	ORDER BY 
		PROJECTTYPE,
		SECTION;
END;
GO